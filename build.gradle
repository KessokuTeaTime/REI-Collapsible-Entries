plugins {
	alias(libs.plugins.fabric.loom)
	id 'maven-publish'
}

archivesBaseName = "${project.archives_base_name}-${libs.versions.minecraft.get()}"
version = project.mod_version
group = project.maven_group

repositories {
	// Quilt Mappings
	maven { url "https://maven.quiltmc.org/repository/release" }

	// REI
	maven { url "https://maven.shedaniel.me" }
}

dependencies {
	// Minecraft
	minecraft(libs.minecraft)

	// Mappings
	mappings(
			variantOf(libs.quilt.mappings) { classifier("intermediary-v2") }
	)

	// Fabric
	modImplementation(libs.fabric.loader)
	modImplementation(libs.fabric.api)

	// REI
	modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}")

	modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}")
	modCompileOnly("me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${project.rei_version}")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = Integer.parseInt(sourceCompatibility)
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
	}
}
